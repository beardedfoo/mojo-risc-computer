module ioshield_debugger (
    input clk,  // clock
    input rst,  // reset
    input io_button[5],
    input io_dip[3][8],
    input debug_imm7[7],
    input debug_imm10[10],
    input debug_data[16],
    input debug_registers[8][16],
    input debug_regA[3],
    input debug_regB[3],
    input debug_regC[3],
    input debug_address[16],
    input debug_opcode[3],
    input exception,
    output io_seg[8],
    output io_sel[4],
    output io_led[3][8],
    output led[8],
    output run
  ) {
  .clk(clk), .rst(rst)  {
    // Condition the step button input and watch for rising edges
    button_press_event center_btn_event;
    button_press_event up_btn_event;
    button_press_event down_btn_event;
    button_press_event left_btn_event;
    button_press_event right_btn_event;
    
    // When set the cpu is made to run
    dff enable_run;

    // The submodes for the register view debug mode
    fsm register_submode = {PC, R0, R1, R2, R3, R4, R5, R6, R7};
    
    // The submodes for the decoder debug mode
    fsm decoder_submode = {INST, OP, REG_A, REG_B, REG_C, IMM7};
    
    // The possible control mode actions
    fsm control_actions = {RUN, STEP};
    
    // A timer which is 0 when the user has not recently switched debug modes
    dff mode_switch_timeout[25];
    
    // The main debug modes
    fsm state = {REGISTER, CONTROL, RUNNING, EXCEPTION, DECODER};
    
    // A controller for the seven segment display on the IO shield
    ioshield_seven_segment_controller seg;
  }
  
  // MODE_STABLE indicates that the debug mode has not been changed "recently"
  const MODE_STABLE = 25b1111111111111111111111111;
  
  // MODE_CHANGED indicates that the debug mode has been changed "recently"
  const MODE_CHANGED = 0;
    
  always {
    // Map the buttons to their event processors
    up_btn_event.in = io_button[0];
    center_btn_event.in = io_button[1];
    down_btn_event.in = io_button[2];
    left_btn_event.in = io_button[3];
    right_btn_event.in = io_button[4];
    
    led = 8h0;
    run = enable_run.q;
    
    // Show the debug address on the 7 segment display    
    io_seg = seg.io_seg;
    io_sel = seg.io_sel;
    seg.data = 4x{8h0};
    seg.display_mode = 4b0000;
    seg.ascii_data = 4x{{8h0}};
    seg.blink = 0;
    io_led = 3x{{8h0}};
    
    // Increment a counter until it rolls around to the MODE_STABLE value
    if (mode_switch_timeout.q != MODE_STABLE) {
      mode_switch_timeout.d = mode_switch_timeout.q + 1;
    }
    
    case (state.q) {   
      state.CONTROL:
        case (control_actions.q) {
          // This menu item puts the machine into a running state
          control_actions.RUN:
            // Enable ASCII display on all digits and show "run" on the screen
            seg.ascii_data[3] = "r";
            seg.ascii_data[2] = "u";
            seg.ascii_data[1] = "n";
            seg.ascii_data[0] = " ";
            seg.display_mode = 4b1111;
            
            // When the center button is pressed put the CPU in a running state
            if (center_btn_event.pressed) {
              enable_run.d = 1;
              mode_switch_timeout.d = MODE_CHANGED;
              state.d = state.RUNNING;
            }
            
            // When the down button is pressed offer the STEP control action
            if (down_btn_event.pressed) {
              mode_switch_timeout.d = MODE_CHANGED;
              control_actions.d = control_actions.STEP;
            }
            
          control_actions.STEP:
            // Enable ASCII display on all digits and show "stp" (sort of) on the screen
            seg.ascii_data[3] = "s";
            seg.ascii_data[2] = "t";
            seg.ascii_data[1] = "e";
            seg.ascii_data[0] = "p";
            seg.display_mode = 4b1111;
            
            // When the center button is pressed step the cpu for one cycle
            if (center_btn_event.pressed) {
              // Enable run in this state (will change next cycle)
              run = 1;
              
              // Switch into the register view debug mode
              state.d = state.REGISTER;
              mode_switch_timeout.d = MODE_CHANGED;
              
              // Start the register view mode by inspecting the PC register
              register_submode.d = register_submode.PC;
            }
            
            // When the up button is pressed offer the RUN control action
            if (up_btn_event.pressed) {
              mode_switch_timeout.d = MODE_CHANGED;
              control_actions.d = control_actions.RUN;
            }
        }
        
        // When the right button is pressed change to the next mode and reset the counter
        if (right_btn_event.pressed) {
          state.d = state.REGISTER;
          mode_switch_timeout.d = MODE_CHANGED;
        }
      
      state.REGISTER:
        case (register_submode.q) {
          // This mode inspects the value of the PC register
          register_submode.PC:
            // Initially show "PC" on the screen, then show the value in PC after
            if (mode_switch_timeout.q != MODE_STABLE) {
              seg.ascii_data[3] = "p";
              seg.ascii_data[2] = "c";
              seg.ascii_data[1] = " ";
              seg.ascii_data[0] = " ";
              seg.display_mode = 4b1111;
            } else {
              seg.data = debug_address;  
            }
              
            // Show the debug data bus on the leds
            io_led[0] = debug_address[7:0];
            io_led[1] = debug_address[15:8];
            
            // Inspect the next register when the down button is pressed
            if (down_btn_event.pressed) {
              register_submode.d = register_submode.R0;
              mode_switch_timeout.d = MODE_CHANGED;
            }
          
          // This mode inspects the value of the R0 register
          register_submode.R0:
            // Initially show the register being inspected on the screen, then show its stored value
            if (mode_switch_timeout.q != MODE_STABLE) {
              seg.ascii_data[3] = "r";
              seg.ascii_data[1] = " ";
              seg.ascii_data[0] = " ";
              seg.display_mode = 4b1011;
              seg.data = 16h0000;
            } else {
              seg.data = debug_registers[0];
            }
            
            // Show the register currently being inspected on the io_led[0] strip
            io_led[0][0] = 1;
            
            // Inspect the previous register when the up button is pressed
            if (up_btn_event.pressed) {
              register_submode.d = register_submode.PC;
              mode_switch_timeout.d = MODE_CHANGED;
            }
            
            // Inspect the next register when the down button is pressed
            if (down_btn_event.pressed) {
              register_submode.d = register_submode.R1;
              mode_switch_timeout.d = MODE_CHANGED;
            }
          
          // This mode inspects the value of the R1 register
          register_submode.R1:
            // Initially show the register being inspected on the screen, then show its stored value
            if (mode_switch_timeout.q != MODE_STABLE) {
              seg.ascii_data[3] = "r";
              seg.ascii_data[1] = " ";
              seg.ascii_data[0] = " ";
              seg.display_mode = 4b1011;
              seg.data = 16h0100;
            } else {
              seg.data = debug_registers[1];
            }
            
            // Show the register currently being inspected on the io_led[0] strip
            io_led[0][1] = 1;
            
            // Inspect the previous register when the up button is pressed
            if (up_btn_event.pressed) {
              register_submode.d = register_submode.R0;
              mode_switch_timeout.d = MODE_CHANGED;
            }
            
            // Inspect the next register when the down button is pressed
            if (down_btn_event.pressed) {
              register_submode.d = register_submode.R2;
              mode_switch_timeout.d = MODE_CHANGED;
            }
            
          // This mode inspects the value of the R2 register
          register_submode.R2:
            // Initially show the register being inspected on the screen, then show its stored value
            if (mode_switch_timeout.q != MODE_STABLE) {
              seg.ascii_data[3] = "r";
              seg.ascii_data[1] = " ";
              seg.ascii_data[0] = " ";
              seg.display_mode = 4b1011;
              seg.data = 16h0200;
            } else {
              seg.data = debug_registers[2];
            }
            
            // Show the register currently being inspected on the io_led[0] strip
            io_led[0][2] = 1;
            
            // Inspect the previous register when the up button is pressed
            if (up_btn_event.pressed) {
              register_submode.d = register_submode.R1;
              mode_switch_timeout.d = MODE_CHANGED;
            }
            
            // Inspect the next register when the down button is pressed
            if (down_btn_event.pressed) {
              register_submode.d = register_submode.R3;
              mode_switch_timeout.d = MODE_CHANGED;
            }
          
          // This mode inspects the value of the R3 register
          register_submode.R3:
            // Initially show the register being inspected on the screen, then show its stored value
            if (mode_switch_timeout.q != MODE_STABLE) {
              seg.ascii_data[3] = "r";
              seg.ascii_data[1] = " ";
              seg.ascii_data[0] = " ";
              seg.display_mode = 4b1011;
              seg.data = 16h0300;
            } else {
              seg.data = debug_registers[3];
            }
            
            // Show the register currently being inspected on the io_led[0] strip
            io_led[0][3] = 1;
            
            // Inspect the previous register when the up button is pressed
            if (up_btn_event.pressed) {
              register_submode.d = register_submode.R2;
              mode_switch_timeout.d = MODE_CHANGED;
            }
            
            // Inspect the next register when the down button is pressed
            if (down_btn_event.pressed) {
              register_submode.d = register_submode.R4;
              mode_switch_timeout.d = MODE_CHANGED;
            }
          
          // This mode inspects the value of the R4 register  
          register_submode.R4:
            // Initially show the register being inspected on the screen, then show its stored value
            if (mode_switch_timeout.q != MODE_STABLE) {
              seg.ascii_data[3] = "r";
              seg.ascii_data[1] = " ";
              seg.ascii_data[0] = " ";
              seg.display_mode = 4b1011;
              seg.data = 16h0400;
            } else {
              seg.data = debug_registers[4];
            }
            
            // Show the register currently being inspected on the io_led[0] strip
            io_led[0][4] = 1;
            
            // Inspect the previous register when the up button is pressed
            if (up_btn_event.pressed) {
              register_submode.d = register_submode.R3;
              mode_switch_timeout.d = MODE_CHANGED;
            }
            
            // Inspect the next register when the down button is pressed
            if (down_btn_event.pressed) {
              register_submode.d = register_submode.R5;
              mode_switch_timeout.d = MODE_CHANGED;
            }
          
          // This mode inspects the value of the R5 register
          register_submode.R5:
            // Initially show the register being inspected on the screen, then show its stored value
            if (mode_switch_timeout.q != MODE_STABLE) {
              seg.ascii_data[3] = "r";
              seg.ascii_data[1] = " ";
              seg.ascii_data[0] = " ";
              seg.display_mode = 4b1011;
              seg.data = 16h0500;
            } else {
              seg.data = debug_registers[5];
            }
            
            // Show the register currently being inspected on the io_led[0] strip
            io_led[0][5] = 1;
            
            // Inspect the previous register when the up button is pressed
            if (up_btn_event.pressed) {
              register_submode.d = register_submode.R4;
              mode_switch_timeout.d = MODE_CHANGED;
            }
            
            // Inspect the next register when the down button is pressed
            if (down_btn_event.pressed) {
              register_submode.d = register_submode.R6;
              mode_switch_timeout.d = MODE_CHANGED;
            }
          
          // This mode inspects the value of the R6 register
          register_submode.R6:
            // Initially show the register being inspected on the screen, then show its stored value
            if (mode_switch_timeout.q != MODE_STABLE) {
              seg.ascii_data[3] = "r";
              seg.ascii_data[1] = " ";
              seg.ascii_data[0] = " ";
              seg.display_mode = 4b1011;
              seg.data = 16h0600;
            } else {
              seg.data = debug_registers[6];
            }
            
            // Show the register currently being inspected on the io_led[0] strip
            io_led[0][6] = 1;
            
            // Inspect the previous register when the up button is pressed
            if (up_btn_event.pressed) {
              register_submode.d = register_submode.R5;
              mode_switch_timeout.d = MODE_CHANGED;
            }
            
            // Inspect the next register when the down button is pressed
            if (down_btn_event.pressed) {
              register_submode.d = register_submode.R7;
              mode_switch_timeout.d = MODE_CHANGED;
            }
            
          // This mode inspects the value of the R7 register
          register_submode.R7:
            // Initially show the register being inspected on the screen, then show its stored value
            if (mode_switch_timeout.q != MODE_STABLE) {
              seg.ascii_data[3] = "r";
              seg.ascii_data[1] = " ";
              seg.ascii_data[0] = " ";
              seg.display_mode = 4b1011;
              seg.data = 16h0700;
            } else {
              seg.data = debug_registers[7];
            }
            
            // Show the register currently being inspected on the io_led[0] strip
            io_led[0][7] = 1;
            
            // Inspect the previous register when the up button is pressed
            if (up_btn_event.pressed) {
              register_submode.d = register_submode.R6;
              mode_switch_timeout.d = MODE_CHANGED;
            }
        }
        
        // When the left button is pressed change to the previous mode
        if (left_btn_event.pressed) {
          state.d = state.CONTROL;
          mode_switch_timeout.d = MODE_CHANGED;
        }
        
        // When the right button is pressed change to the next mode
        if (right_btn_event.pressed) {
          state.d = state.DECODER;
          mode_switch_timeout.d = MODE_CHANGED;
        }
        
      state.DECODER:
        case (decoder_submode.q) {
          decoder_submode.INST:
            case (debug_opcode) {
              Opcode.ADD:
                seg.ascii_data[3] = "a";
                seg.ascii_data[2] = "d";
                seg.ascii_data[1] = "d";
                seg.ascii_data[0] = " ";
                seg.display_mode = 4b1111;
                
              Opcode.ADDI:
                seg.ascii_data[3] = "a";
                seg.ascii_data[2] = "d";
                seg.ascii_data[1] = "d";
                seg.ascii_data[0] = "i";
                seg.display_mode = 4b1111;
                
              Opcode.JALR:
                seg.ascii_data[3] = "j";
                seg.ascii_data[2] = "a";
                seg.ascii_data[1] = "l";
                seg.ascii_data[0] = "r";
                seg.display_mode = 4b1111;
                
              default:
                seg.ascii_data[3] = "-";
                seg.ascii_data[2] = "-";
                seg.ascii_data[1] = "-";
                seg.ascii_data[0] = "-";
                seg.display_mode = 4b1111;
            }
            
            if (down_btn_event.pressed) {
              decoder_submode.d = decoder_submode.OP;
              mode_switch_timeout.d = MODE_CHANGED;
            }
          
          decoder_submode.OP:
            seg.ascii_data[3] = "o";
            seg.ascii_data[2] = "p";
            seg.ascii_data[1] = " ";
            seg.display_mode = 4b1110;
            seg.data = debug_opcode;
            
            if (up_btn_event.pressed) {
              decoder_submode.d = decoder_submode.INST;
              mode_switch_timeout.d = MODE_CHANGED;
            }
            
            if (down_btn_event.pressed) {
              decoder_submode.d = decoder_submode.REG_A;
              mode_switch_timeout.d = MODE_CHANGED;
            }
            
          decoder_submode.REG_A:
            seg.ascii_data[3] = "r";
            seg.ascii_data[2] = "a";
            seg.ascii_data[1] = " ";
            seg.display_mode = 4b1110;
            seg.data = debug_regA;
            
            if (up_btn_event.pressed) {
              decoder_submode.d = decoder_submode.OP;
              mode_switch_timeout.d = MODE_CHANGED;
            }
            
            
            if (down_btn_event.pressed) {
              decoder_submode.d = decoder_submode.REG_B;
              mode_switch_timeout.d = MODE_CHANGED;
            }
            
          decoder_submode.REG_B:
            seg.ascii_data[3] = "r";
            seg.ascii_data[2] = "b";
            seg.ascii_data[1] = " ";
            seg.display_mode = 4b1110;
            seg.data = debug_regB;
            
            if (up_btn_event.pressed) {
              decoder_submode.d = decoder_submode.REG_A;
              mode_switch_timeout.d = MODE_CHANGED;
            }
            
            
            if (down_btn_event.pressed) {
              decoder_submode.d = decoder_submode.REG_C;
              mode_switch_timeout.d = MODE_CHANGED;
            }
            
          decoder_submode.REG_C:
            seg.ascii_data[3] = "r";
            seg.ascii_data[2] = "c";
            seg.ascii_data[1] = " ";
            seg.display_mode = 4b1110;
            seg.data = debug_regC;
            
            if (up_btn_event.pressed) {
              decoder_submode.d = decoder_submode.REG_B;
              mode_switch_timeout.d = MODE_CHANGED;
            }
            
            if (down_btn_event.pressed) {
              decoder_submode.d = decoder_submode.IMM7;
              mode_switch_timeout.d = MODE_CHANGED;
            }
            
          decoder_submode.IMM7:
            if (mode_switch_timeout.q != MODE_STABLE) {
              seg.ascii_data[3] = "i";
              seg.ascii_data[1] = " ";
              seg.ascii_data[0] = " ";
              seg.display_mode = 4b1011;
              seg.data = 16h0700;
            } else {
              seg.data = debug_imm7;
            }
            
            if (up_btn_event.pressed) {
              decoder_submode.d = decoder_submode.REG_C;
              mode_switch_timeout.d = MODE_CHANGED;
            }
        }
        
        // When the left button is pressed change to the previous mode
        if (left_btn_event.pressed) {
          state.d = state.REGISTER;
          mode_switch_timeout.d = MODE_CHANGED;
        }
        
      state.RUNNING:
        // Show the ASCII string "...." on the screen while the cpu is running
        seg.ascii_data = "....";
        seg.display_mode = 4b1111;
        
        // Show the instruction address pattern (animated) on the IO shield panel
        io_led[0] = debug_address[7:0];
        io_led[1] = debug_address[15:8];
        
        // When the center button is pressed, drop into the debugger
        if (center_btn_event.pressed) {
          // Stop the CPU from processing more instructions
          enable_run.d = 0;
          
          // Change into register view mode
          mode_switch_timeout.d = MODE_CHANGED;
          state.d = state.REGISTER;
          
          // View the PC register initially
          register_submode.d = register_submode.PC;
        }
        
        // When the machine encounters an exception switch into a mode which handles that
        if (exception) {
          state.d = state.EXCEPTION;
          mode_switch_timeout.d = MODE_CHANGED;
        }
        
      state.EXCEPTION:
        // Enable ASCII mode on all four digits and show "Err" on the screen
        seg.ascii_data[3] = "e";
        seg.ascii_data[2] = "r";
        seg.ascii_data[1] = "r";
        seg.ascii_data[0] = " ";
        seg.display_mode = 4b1111;
        
        // Blink the display to grab the user's attention
        seg.blink = 1;
        
        // The run pin should no longer be active
        enable_run.d = 0;

        // By pressing the center button the user acknowledges the exception and enters the debugger
        if (center_btn_event.pressed) {
          // Enter the debugger in the register view mode
          state.d = state.REGISTER;
          mode_switch_timeout.d = MODE_CHANGED;
          
          // Start the register view mode by inspecting the PC register
          register_submode.d = register_submode.PC;
        }
    }
  }
}
